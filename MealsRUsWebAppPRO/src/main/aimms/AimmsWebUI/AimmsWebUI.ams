## ams_version=1.0

LibraryModule AimmsWebUI {
    Prefix: webui;
    Interface: Public_Declarations;
    Section Data_Management {
        DeclarationSection Public_Declarations {
            Set AllCases {
                Text: "All available WebUI cases (local cases as wel as cases stored on the PRO server)";
                Index: IndexCases;
            }
            ElementParameter CurrentCase {
                Text: "Reference to the latest touched (i.e. created, saved or loaded) WebUI case";
                Range: AllCases;
            }
            StringParameter CasePath {
                IndexDomain: IndexCases;
            }
            Parameter CaseReadOnly {
                IndexDomain: IndexCases;
            }
            StringParameter CurrentCasePath;
            Parameter CurrentCaseIsDirty;
        }
        Function GetCaseStorageServerPath {
            Range: string;
            Body: {
                GetCaseStorageServerPath := FormatString("PRO:/UserData/%s/%s/Cases/%s",pro::PROEnvironment,pro::PROUserName,pro::ModelName);
            }
        }
        Function GetBareCasename {
            Arguments: (casename,protocol);
            Range: string;
            Body: {
                empty BareCasename;
                
                casenameLen := StringLength(casename);
                protocolLen := StringLength(protocol);
                casenameLowerCase := StringToLower(casename);
                protocolLowerCase := StringToLower(protocol);
                
                if ( SubString(casenameLowerCase,1,protocolLen) = protocolLowerCase and SubString(casenameLowerCase,casenameLen-4,casenameLen) = ".data" ) then
                	numSlashes := StringOccurrences(casename,"/");
                	pos := FindNthString(casename,"/",numSlashes);
                	BareCasename := SubString(casename,pos+1,casenameLen-5);
                endif;
                
                GetBareCasename := bareCasename;
            }
            StringParameter casename {
                Property: Input;
            }
            Parameter casenameLen;
            Parameter protocolLen;
            StringParameter casenameLowerCase;
            StringParameter protocolLowerCase;
            Parameter numSlashes;
            Parameter pos;
            StringParameter bareCasename;
            StringParameter protocol {
                Property: Input;
            }
        }
        Procedure RefreshCurrentCase {
            Arguments: (casename);
            Body: {
                CurrentCase := First( IndexCases | CasePath(IndexCases) = casename );
                if ( CurrentCase ) then
                	CurrentCasePath := CasePath(CurrentCase);
                else
                	CurrentCasePath := "";
                endif;
                CurrentCaseIsDirty := 0;
            }
            StringParameter casename {
                Property: Input;
            }
        }
        Procedure RefreshAllCases {
            Body: {
                empty AllCases, LocalFileNumbers, casecount;
                
                if ( DirectoryExists("Data") ) then
                	DirectoryGetFiles("Data","*.data",LocalFileNames);
                endif;
                
                PROIsInitialized := pro::management::IsInitialized;
                
                for (fn) do
                	casename := FormatString("local-case-%n",Ord(fn));
                	SetElementAdd(AllCases,aCase,casename);
                	CasePath(aCase) := FormatString("file:/%s",LocalFileNames(fn));
                	CaseReadOnly(aCase) := if ( PROIsInitialized ) then 1 else 0 endif;
                endfor;
                
                StoragePath := GetCaseStorageServerPath + "/";
                ! strip "PRO:" from path
                len := StringLength(StoragePath);
                StoragePath := SubString(StoragePath,5,len-1);
                
                if ( PROIsInitialized ) then
                	pro::management::LocalLogInfo("List WebUI Cases on PRO server in path " + StoragePath);
                	pro::storage::ListObjects(StoragePath,PROCases,PROCaseName,PROCaseType,PROCaseSize,PROCaseAuthorization);
                	! filter out all actual cases files and store them in the public set ManagedSessionListedCases
                	for (pc) do
                		casename := FormatString("%e",pc);
                		len := StringLength(casename);
                		if ( SubString(casename,len-4,len) = ".data" ) then
                			casename := FormatString("server-case-%n",casecount+1);
                			SetElementAdd(AllCases,aCase,casename);
                			CasePath(aCase) := FormatString("pro:/%e",pc);
                			CaseReadOnly(aCase) := 0;
                			casecount += 1;
                		endif;
                	endfor;
                endif;
            }
            Parameter PROIsInitialized;
            Set LocalFileNumbers {
                SubsetOf: Integers;
                Index: fn;
            }
            StringParameter LocalFileNames {
                IndexDomain: fn;
            }
            StringParameter StoragePath;
            Parameter len;
            ElementParameter aCase {
                Range: AllCases;
            }
            Parameter casecount;
            StringParameter casename;
            Set PROCases {
                Index: pc;
            }
            StringParameter PROCaseName {
                IndexDomain: pc;
            }
            StringParameter PROCaseType {
                IndexDomain: pc;
            }
            Parameter PROCaseSize {
                IndexDomain: pc;
            }
            StringParameter PROCaseAuthorization {
                IndexDomain: pc;
            }
        }
        Procedure LoadCase {
            Arguments: (casename);
            Body: {
                empty success, caseUrl;
                
                bareCasename := GetBareCasename(casename,"PRO:/");
                if ( StringLength(bareCasename) > 0 ) then
                	! PRO Storage case
                	storageServerPath := GetCaseStorageServerPath;
                	caseUrl := FormatString("%s/%s.data",storageServerPath,bareCasename);
                else
                	bareCasename := GetBareCasename(casename,"file:/");
                	if ( StringLength(bareCasename) > 0 ) then
                		! Case in project folder
                		caseUrl := FormatString("./Data/%s.data",bareCasename);
                	endif;
                endif;
                
                if ( StringLength(caseUrl) > 0 ) then
                	pro::management::LocalLogInfo("Loading WebUI case from : " + caseUrl);
                	if ( CaseFileLoad(caseUrl) ) then
                		success := 1;
                	endif;
                endif;
                
                if ( success ) then
                	RefreshAllCases;
                	RefreshCurrentCase(casename);
                endif;
                
                return success;
            }
            StringParameter casename {
                Property: Input;
            }
            StringParameter bareCasename;
            StringParameter storageServerPath;
            StringParameter caseUrl;
            Parameter success;
        }
        Procedure SaveCase {
            Arguments: (casename);
            Body: {
                empty success, caseUrl;
                
                bareCasename := GetBareCasename(casename,"PRO:/");
                if ( StringLength(bareCasename) > 0 ) then
                	! PRO Storage case
                	storageServerPath := GetCaseStorageServerPath;
                	caseUrl := FormatString("%s/%s.data",storageServerPath,bareCasename);
                else
                	bareCasename := GetBareCasename(casename,"file:/");
                	if ( StringLength(bareCasename) > 0 ) then
                		! Case in project folder
                		if ( not DirectoryExists("Data") ) then
                			DirectoryCreate("Data");
                		endif;
                		caseUrl := FormatString("./Data/%s.data",bareCasename);
                	endif;
                endif;
                
                if ( StringLength(caseUrl) > 0 ) then
                	StoredIdentifiers := AllIdentifiers;
                	StoredIdentifiers -= 'webui::CurrentCaseIsDirty';
                	StoredIdentifiers -= 'webui::CurrentCasePath';
                	pro::management::LocalLogInfo("Saving WebUI case to : " + caseUrl);
                	if ( CaseFileSave(caseUrl,StoredIdentifiers) ) then
                		success := 1;
                	endif;
                endif;
                
                if ( success ) then
                	RefreshAllCases;
                	RefreshCurrentCase(casename);
                endif;
                
                return success;
            }
            StringParameter casename {
                Property: Input;
            }
            StringParameter bareCasename;
            StringParameter storageServerPath;
            StringParameter caseUrl;
            Parameter success;
            Set StoredIdentifiers {
                SubsetOf: AllIdentifiers;
            }
        }
        Procedure ExistsCase {
            Arguments: (casename);
            Body: {
                empty numberOfVersions;
                
                bareCasename := GetBareCasename(casename,"PRO:/");
                if ( StringLength(bareCasename) > 0 ) then
                	! PRO Storage case
                	bareCaseName := FormatString("%s.data",bareCasename);
                	storageServerPath := GetCaseStorageServerPath + "/";
                	! strip "PRO:" from path
                	len := StringLength(storageServerPath);
                	storageServerPath := SubString(storageServerPath,5,len-1);
                	pro::storage::GetObjectInfo(storageServerPath,bareCaseName,objFileName,objFileType,numberOfVersions,authorization,lastVersionID);
                	logInfo := FormatString("Exists WebUI PRO storage case : (%s,%s) = %n",storageServerPath,bareCaseName,numberOfVersions);
                	pro::management::LocalLogInfo(logInfo);
                else
                	bareCasename := GetBareCasename(casename,"file:/");
                	if ( StringLength(bareCasename) > 0 ) then
                		! Case in project folder
                		pro::management::LocalLogInfo("Exists WebUI local case : " + caseUrl);
                		caseUrl := FormatString("./Data/%s.data",bareCasename);
                		numberOfVersions := FileExists(caseUrl);
                		logInfo := FormatString("Exists WebUI local case : %s = %n",caseUrl,numberOfVersions);
                		pro::management::LocalLogInfo(logInfo);
                	endif;
                endif;
                
                if ( numberOfVersions > 0 ) then
                	return 1;
                else
                	return 0;
                endif;
            }
            StringParameter casename {
                Property: Input;
            }
            StringParameter bareCasename;
            StringParameter storageServerPath;
            Parameter len;
            Parameter numberOfVersions;
            StringParameter objFileName;
            StringParameter objFileType;
            StringParameter authorization;
            StringParameter lastVersionID;
            StringParameter logInfo;
            StringParameter caseUrl;
        }
        Procedure CreateCase {
            Arguments: (casename);
            Body: {
                numberOfversions := ExistsCase(casename);
                if ( numberOfversions > 0 ) then
                	return 0;
                else
                	return SaveCase(casename);
                endif;
            }
            StringParameter casename {
                Property: Input;
            }
            Parameter numberOfVersions;
        }
        Procedure DeleteCase {
            Arguments: (casename);
            Body: {
                empty success;
                
                if ( ExistsCase(casename) ) then
                
                	bareCasename := GetBareCasename(casename,"PRO:/");
                	if ( StringLength(bareCasename) > 0 ) then
                		! PRO Storage case
                		bareCaseName := FormatString("%s.data",bareCasename);
                		storageServerPath := GetCaseStorageServerPath + "/";
                		! strip "PRO:" from path
                		len := StringLength(storageServerPath);
                		storageServerPath := SubString(storageServerPath,5,len-1);
                		pro::storage::DeleteObjectAllVersions(storageServerPath,bareCasename);
                		success := 1;
                	else
                		bareCasename := GetBareCasename(casename,"file:/");
                		if ( StringLength(bareCasename) > 0 ) then
                			! Case in project folder
                			caseUrl := FormatString("./Data/%s.data",bareCasename);
                			if ( FileDelete(caseUrl) ) then
                				success := 1;
                			endif;
                		endif;
                	endif;
                
                	if ( success ) then
                		RefreshAllCases;
                		RefreshCurrentCase("<empty>");
                	endif;
                
                endif;
                
                return success;
            }
            StringParameter casename {
                Property: Input;
            }
            StringParameter bareCasename;
            StringParameter storageServerPath;
            Parameter len;
            StringParameter caseUrl;
            Parameter success;
        }
    }
    Procedure LibraryInitialization;
    Procedure LibraryTermination {
        Body: {
            return 1 ;
        }
    }
    Procedure PostLibraryInitialization {
        Body: {
            RefreshAllCases;
        }
    }
}
